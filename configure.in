dnl Process this file with autoconf to produce a configure script.
AC_REVISION([$Id: configure.in,v 1.13 2005/07/03 01:17:19 kvance Exp $])
AC_INIT(KevEdit, 0.5.1, bitman@users.sourceforge.net, kevedit)
AC_CONFIG_SRCDIR([src/kevedit/kevedit.c])
AC_CONFIG_HEADER([src/config.h])

dnl Detect host and target environments (target is needed by AM_PATH_SDL)
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

dnl Use automake
AM_INIT_AUTOMAKE
AM_MAINTAINER_MODE

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_RANLIB

dnl Determine available displays
AC_MSG_NOTICE(Searching for available displays)

dnl Check for SDL
AC_ARG_WITH(sdl,
	[  --without-sdl           SDL support (default with)])

if test "x$with_sdl" != "xno"
then
	AM_PATH_SDL(1.2.0, [AC_MSG_NOTICE(Using SDL display and sound)
			   sdl=true],
			   [AC_MSG_NOTICE(SDL not available for display)])

dnl This must be done before most checks because it can affect what headers
dnl and libraries are available
CFLAGS="$CFLAGS $SDL_CFLAGS"
LIBS="$LIBS $SDL_LIBS"

if test x$sdl = xtrue; then
	AC_DEFINE(SDL, [1], [Define to 1 if SDL is available])
	display_objects="$display_objects display_sdl.o"
	synth_objects="$synth_objects sdl_synth.o"
fi
#AM_CONDITIONAL(SDL, [test x$sdl = xtrue])
fi

dnl Check for DOS display
case "$host" in
	*-*-msdos*)
		AC_MSG_NOTICE(Using DOS display and pcspeaker sound)
		dos=true
		;;
	*)
		AC_MSG_NOTICE(DOS display not available)
		;;
esac

if test x$dos = xtrue; then
	AC_DEFINE(DOS, [1], [Define to 1 if host is DOS])
	display_objects="$display_objects display_dos.o"
	synth_objects="$synth_objects pcspeaker.o"
fi
#AM_CONDITIONAL(DOS, [test x$dos = xtrue])

dnl Check for Linux, to enable DOSBox support
case "$host" in
	*-*-linux*)
		AC_MSG_NOTICE(Linux detected: enabling DOSBox support)
		linux=true
		;;
	*)
		AC_MSG_NOTICE(Not enabling DOSBox support)
		;;
esac

if test x$linux = xtrue; then
	AC_DEFINE(DOSBOX, [1], [Define to 1 if DOSBox is enabled])
fi

AC_SUBST([display_objects])
AC_SUBST([synth_objects])

dnl Check for windows resource compiler.
AC_CHECK_PROGS(WINDRES, windres)
AC_SUBST(WINDRES)
AM_CONDITIONAL(HAVE_WINDRES, [test x$WINDRES != x])

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_DIRENT
dnl AC_CHECK_HEADERS(strings.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
dnl AC_C_CONST
dnl AC_TYPE_SIZE_T
dnl AC_HEADER_TIME

AC_CHECK_TYPES([int8_t, int16_t, int32_t, u_int8_t, u_int16_t, u_int32_t])

dnl Checks for library functions.
dnl AC_FUNC_ALLOCA
dnl AC_FUNC_FNMATCH
AC_TYPE_SIGNAL
dnl AC_FUNC_STRCOLL
AC_CHECK_FUNCS(getcwd strdup strstr)

dnl Check for math library
AC_CHECK_LIB(m, floor)

dnl Check for availability of long file names
AC_SYS_LONG_FILE_NAMES

dnl Check for windows (for glob's sake)
AC_MSG_CHECKING(for Windows)
case "$host" in
	*-*-*cygwin*)
		AC_DEFINE(WINDOWS32, [1], [Define to 1 if host is Windows])
		AC_MSG_RESULT(yes: cygwin)
		;;
	*-*-*mingw32*)
		AC_DEFINE(WINDOWS32, [1], [Define to 1 if host is Windows])
		AC_MSG_RESULT(yes: MingW)
		;;
	*)
		AC_MSG_RESULT(no)
		;;
esac

dnl Check for glob and add it to CPPFLAGS
AC_PROG_GNU_GLOB
CPPFLAGS="$CPPFLAGS $GLOBINC"

dnl Add some useful include paths
AC_SUBST(INCLUDE_PATHS) INCLUDE_PATHS='-I$(top_srcdir)/src -I$(top_builddir)'
CPPFLAGS="$CPPFLAGS $INCLUDE_PATHS"

AC_CONFIG_FILES([docs/Makefile
		 dosbox/Makefile
                 glob/Makefile
                 inst/kevedit.spec
                 src/libzzt2/Makefile
                 src/structures/Makefile
                 src/display/Makefile
                 src/synth/Makefile
                 src/themes/Makefile
                 src/themes/rusty/Makefile
                 src/texteditor/Makefile
                 src/help/Makefile
                 src/dialogs/Makefile
                 src/zlaunch/Makefile
                 src/kevedit/Makefile
                 src/Makefile
                 Makefile
                 buildinfo.h])

AC_OUTPUT
